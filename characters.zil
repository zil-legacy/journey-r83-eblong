"CHARACTERS for
		      		JOURNEY
	(c) Copyright 1988, 1989 Infocom, Inc.  All Rights Reserved."

<BEGIN-SEGMENT 0>

<CONSTANT PRAXIXBIT 1>
<CONSTANT BERGONBIT 2>
<CONSTANT ESHERBIT 4>
<CONSTANT HURTHBIT 8>
<CONSTANT TAGBIT 16>
<CONSTANT MINARBIT 32>
<CONSTANT UMBERBIT 64>
<CONSTANT ALLBITS 255>

<OBJECT PRAXIX
	(DESC %<COMPAD "Praxix" ,NAMPADSTR>)
	(SDESC "Praxix")
	(KBD %<ASCII !\P>)
	(CBIT PRAXIXBIT)
	(ROBJ HIMSELF-OBJECT)
	(FLAGS IN-PARTY)
	(TRAVEL-COMMANDS CAST-COMMAND EXAMINE-COMMAND NUL-COMMAND)
	(PROVISION-COMMANDS EXAMINE-COMMAND NUL-COMMAND NUL-COMMAND)
	(FIGHT-COMMANDS CAST-COMMAND NUL-COMMAND NUL-COMMAND)
	(OPTION-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(SAVED-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(ATTACK 10 10)
	(DEFEND 0 30)
	(STATUS 100)>

<OBJECT BERGON
	(DESC %<COMPAD "Bergon" ,NAMPADSTR>)
	(SDESC "Bergon")
	(KBD %<ASCII !\B>)
	(CBIT BERGONBIT)
	(FLAGS IN-PARTY)
	(TRAVEL-COMMANDS GET-ADVICE-COMMAND NUL-COMMAND NUL-COMMAND)
	(PROVISION-COMMANDS GET-ADVICE-COMMAND NUL-COMMAND NUL-COMMAND)
	(FIGHT-COMMANDS FLANK-COMMAND NUL-COMMAND NUL-COMMAND)
	(OPTION-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(SAVED-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(ROBJ BERGON-OBJECT)
	(REMOVE
	 <EXECUTE ()
		  <SETG LEADER <FIRST-IN-PARTY ,ESHER ,HURTH ,MINAR ,PRAXIX>>
		  <ADD-TRAVEL-COMMAND ,LEADER ,GET-ADVICE-COMMAND>>)
	(ADD
	 <EXECUTE ()
		  <REMOVE-TRAVEL-COMMAND ,LEADER ,GET-ADVICE-COMMAND>
		  <SETG LEADER ,BERGON>>)
	(ATTACK 40 30)
	(DEFEND 20 50)
	(STATUS 100)>

<OBJECT BERGON-OBJECT
	(SDESC "Bergon")
	(LOC RANDOM-OBJECTS)
	(KBD %<ASCII !\B>)
	(FLAGS DONT-TAKE)>

<OBJECT ESHER-OBJECT
	(SDESC "Esher")
	(LOC RANDOM-OBJECTS)
	(KBD %<ASCII !\E>)
	(FLAGS DONT-TAKE)>

<OBJECT MINAR-OBJECT
	(SDESC "Minar")
	(KBD %<ASCII !\M>)
	(LOC RANDOM-OBJECTS)
	(FLAGS DONT-TAKE)>

<OBJECT HURTH-OBJECT
	(SDESC "Hurth")
	(KBD %<ASCII !\H>)
	(LOC RANDOM-OBJECTS)
	(FLAGS DONT-TAKE)>

<OBJECT TAG-OBJECT
	(SDESC "Tag")
	(KBD %<ASCII !\T>)
	(LOC RANDOM-OBJECTS)
	(FLAGS DONT-TAKE)>

<OBJECT HIMSELF-OBJECT
	(SDESC "himself")
	(KBD %<ASCII !\H>)
	(LOC RANDOM-OBJECTS)
	(FLAGS DONT-TAKE)>

<OBJECT ESHER
	(DESC %<COMPAD "Esher" ,NAMPADSTR>)
	(SDESC "Esher")
	(KBD %<ASCII !\E>)
	(CBIT ESHERBIT)
	(FLAGS IN-PARTY)
	(ROBJ ESHER-OBJECT)
	(TRAVEL-COMMANDS EXAMINE-COMMAND SCOUT-COMMAND NUL-COMMAND)
	(PROVISION-COMMANDS LOOK-AROUND-COMMAND NUL-COMMAND NUL-COMMAND)
	(FIGHT-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(OPTION-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(SAVED-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(ADD
	 <EXECUTE ()
		  <COND (<NOT <FIRST-IN-PARTY ,HURTH ,MINAR>>
			 <ADD-TRAVEL-COMMAND ,ESHER ,SCOUT-COMMAND>
			 <ADD-PROVISION-COMMAND ,ESHER ,LOOK-AROUND-COMMAND>)>>)
	(REMOVE
	 <EXECUTE ()
		  <COND (<EQUAL? ,ESHER ,LEADER>
			 <SETG LEADER
			       <FIRST-IN-PARTY ,BERGON
					       ,HURTH
					       ,MINAR
					       ,PRAXIX>>)>>)
	(ATTACK 20 10)
	(DEFEND 10 35)
	(STATUS 100)>

<OBJECT MINAR
	(DESC %<COMPAD "Minar" ,NAMPADSTR>)
	(SDESC "Minar")
	(KBD %<ASCII !\M>)
	(CBIT MINARBIT)
	(ROBJ MINAR-OBJECT)
	(TRAVEL-COMMANDS SCOUT-COMMAND NUL-COMMAND NUL-COMMAND)
	(PROVISION-COMMANDS LOOK-AROUND-COMMAND NUL-COMMAND NUL-COMMAND)
	(FIGHT-COMMANDS FLANK-COMMAND NUL-COMMAND NUL-COMMAND)
	(OPTION-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(SAVED-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(ADD
	 <EXECUTE ()
		  <FSET ,MINAR ,DONT-DROP>
		  <COND (<AND <NOT <IN-PARTY? ,HURTH>>
			      <IN-PARTY? ,ESHER>>
			 <REMOVE-TRAVEL-COMMAND ,ESHER ,SCOUT-COMMAND>
			 <REMOVE-PROVISION-COMMAND ,ESHER ,LOOK-AROUND-COMMAND>)>>)
	(REMOVE
	 <EXECUTE ()
		  <COND (<AND <NOT <IN-PARTY? ,HURTH>>
			      <IN-PARTY? ,ESHER>>
			 <ADD-TRAVEL-COMMAND ,ESHER ,SCOUT-COMMAND>
			 <ADD-PROVISION-COMMAND ,ESHER ,LOOK-AROUND-COMMAND>)>>)
	(ATTACK 30 20)
	(DEFEND 10 40)
	(STATUS 100)>

<OBJECT HURTH
	(DESC %<COMPAD "Hurth" ,NAMPADSTR>)
	(SDESC "Hurth")
	(KBD %<ASCII !\H>)
	(CBIT HURTHBIT)
	(ROBJ HURTH-OBJECT)
	(TRAVEL-COMMANDS SCOUT-COMMAND TELL-STORY-COMMAND EXAMINE-COMMAND)
	(PROVISION-COMMANDS LOOK-AROUND-COMMAND NUL-COMMAND NUL-COMMAND)
	(FIGHT-COMMANDS FLANK-COMMAND NUL-COMMAND NUL-COMMAND)
	(OPTION-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(SAVED-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(REMOVE
	 <EXECUTE ("AUX" F)
		  <COND (<EQUAL? ,HURTH ,LEADER>
			 <SETG LEADER
			       <FIRST-IN-PARTY ,BERGON
					       ,ESHER
					       ,MINAR
					       ,PRAXIX>>)> 
		  <COND (<SET F <FIRST-IN-PARTY ,MINAR ,ESHER>>
			 <ADD-TRAVEL-COMMAND .F ,SCOUT-COMMAND>
			 <ADD-PROVISION-COMMAND .F ,LOOK-AROUND-COMMAND>)>>)
	(ADD
	 <EXECUTE ("AUX" F)
		  <COND (<SET F <FIRST-IN-PARTY ,MINAR ,ESHER>>
			 <REMOVE-TRAVEL-COMMAND .F ,SCOUT-COMMAND>
			 <REMOVE-PROVISION-COMMAND .F ,LOOK-AROUND-COMMAND>)>>)
	(ATTACK 30 20)
	(DEFEND 10 40)
	(STATUS 100)>

<OBJECT UMBER
	(DESC %<COMPAD "Umber" ,NAMPADSTR>)
	(SDESC "Umber")
	(KBD %<ASCII !\U>)
	(CBIT UMBERBIT)
	(TRAVEL-COMMANDS TELL-STORY-COMMAND NUL-COMMAND NUL-COMMAND)
	(PROVISION-COMMANDS LOOK-AROUND-COMMAND NUL-COMMAND NUL-COMMAND)
	(FIGHT-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(OPTION-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(SAVED-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(ATTACK 30 20)
	(DEFEND 10 40)
	(STATUS 100)>

<OBJECT TAG
	(DESC %<COMPAD "Tag" ,NAMPADSTR>)
	(SDESC "Tag")
	(KBD %<ASCII !\T>)
	(CBIT TAGBIT)
	(FLAGS IN-PARTY)
	(TRAVEL-COMMANDS PICK-UP-COMMAND DROP-COMMAND INVENTORY-COMMAND)
	(PROVISION-COMMANDS BUY-COMMAND SELL-COMMAND INVENTORY-COMMAND)
	(FIGHT-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(OPTION-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(SAVED-COMMANDS NUL-COMMAND NUL-COMMAND NUL-COMMAND)
	(ATTACK 25 10)
	(DEFEND 10 35)
	(STATUS 100)>

<GLOBAL PARTY <TABLE 5 BERGON PRAXIX ESHER TAG 0>>

<GLOBAL LEADER 0>

<ROUTINE CLEAR-PARTY-BIT (BIT "AUX" (OFF 0))
	 <REPEAT ()
		 <COND (<G? <SET OFF <+ .OFF 1>> ,PARTY-MAX>
			<RETURN>)
		       (T
			<FCLEAR <GET ,PARTY .OFF> .BIT>)>>>

<ROUTINE MAKE-UNBUSY (ACT)
	 <FCLEAR .ACT ,BUSY>
	 <SETG UPDATE-FLAG T>>

<ROUTINE MAKE-BUSY (ACT "OPTIONAL" (CMD ,BUSY-COMMAND) "AUX" OFF TBL)
	 <COND (<SET OFF <PARTY-PCM .ACT>>
		<FSET .ACT ,BUSY>
		<SET TBL <GET ,CHARACTER-INPUT-TBL .OFF>>
		<PUT .TBL 0 .CMD>
		<PUT .TBL 1 ,NUL-COMMAND>
		<PUT .TBL 2 ,NUL-COMMAND>
		<NORMAL-ALL .OFF>)>>

<ROUTINE CLEAR-BUSY () <CLEAR-PARTY-BIT ,BUSY>>

<ROUTINE NEW-CHARACTER (CHR "AUX" (ACT ,ACTOR))
	 <PURIFY-CHARACTER .CHR>
	 <SETG UPDATE-FLAG T>
	 <PRINT-CHARACTER-COMMANDS>
	 <SETG ACTOR .ACT>>

<ROUTINE PURIFY-CHARACTER (CHR)
	 <FCLEAR .CHR ,BUSY>
	 <FCLEAR .CHR ,SUBGROUP>
	 <PUTP .CHR ,P?STATUS 100>>

<ROUTINE PARTY-ADD (CHR "AUX" NC)
	 <SET NC <+ ,PARTY-MAX 1>>
	 <COND (<G? .NC 5>
		<TELL "[Error: Attempt to add 6th party member]" D .CHR>
		<RTRUE>)>
	 <PUT ,PARTY ,PARTY-MAX .CHR>
	 <PUT ,PARTY .NC ,TAG>
	 <SETG PARTY-MAX .NC>
	 <FSET .CHR ,IN-PARTY>
	 <SORT-PARTY>
	 <NEW-CHARACTER .CHR>
	 <SET NC <APPLY <GETP .CHR ,P?ADD>>>
	 <SETG UPDATE-FLAG T>
	 <RTRUE>>

<ROUTINE PARTY-CHANGE (CHR1 CHR2 "AUX" (CNT <GET ,PARTY 0>) TMP)
	 <REPEAT ()
		 <COND (<L? .CNT 1> <RFALSE>)
		       (<EQUAL? <GET ,PARTY .CNT> .CHR1>
			<PUT ,PARTY .CNT .CHR2>
			<FSET .CHR2 ,IN-PARTY>
			<FCLEAR .CHR1 ,IN-PARTY>
			<SORT-PARTY>
			<NEW-CHARACTER .CHR2>
			<SET TMP <APPLY <GETP .CHR1 ,P?REMOVE>>>
			<SET TMP <APPLY <GETP .CHR2 ,P?ADD>>>
			<SETG UPDATE-FLAG T>
			<RTRUE>)>
		 <SET CNT <- .CNT 1>>>>

<ROUTINE PARTY-REMOVE (CHR "AUX" (CNT 0) C ACT)
	 <SET ACT ,ACTOR>
	 <REPEAT ()
		 <COND (<G? <SET CNT <+ .CNT 1>> ,PARTY-MAX>
			<RFALSE>)
		       (<EQUAL? <GET ,PARTY .CNT> .CHR>
			<COPYT <REST ,PARTY <* <+ .CNT 1> 2>>
			       <REST ,PARTY <* .CNT 2>>
			       <* <- ,PARTY-MAX .CNT> 2>>
			<COPYT <REST ,PARTY <* ,PARTY-MAX 2>> 0 2>
			<SETG PARTY-MAX <- ,PARTY-MAX 1>>
			<FCLEAR .CHR ,IN-PARTY>
			<SORT-PARTY>
			<SETG UPDATE-FLAG T>
			<PRINT-CHARACTER-COMMANDS>
			<SETG ACTOR .ACT>
			<SET C <APPLY <GETP .CHR ,P?REMOVE>>>
			<RTRUE>)>>>
	 
<GLOBAL PARTY-SORT-TBL <LTABLE 0 0 0 0 0>>

<ROUTINE SORT-PARTY ("AUX" (CNT 1))
	 <COND (<IN-PARTY? ,BERGON>
		<PUT ,PARTY-SORT-TBL .CNT ,BERGON>
		<SET CNT <+ .CNT 1>>)>
	 <COND (<IN-PARTY? ,PRAXIX>
		<PUT ,PARTY-SORT-TBL .CNT ,PRAXIX>
		<SET CNT <+ .CNT 1>>)>
	 <COND (<IN-PARTY? ,ESHER>
		<PUT ,PARTY-SORT-TBL .CNT ,ESHER>
		<SET CNT <+ .CNT 1>>)>
	 <COND (<IN-PARTY? ,MINAR>
		<PUT ,PARTY-SORT-TBL .CNT ,MINAR>
		<SET CNT <+ .CNT 1>>)>
	 <COND (<IN-PARTY? ,HURTH>
		<PUT ,PARTY-SORT-TBL .CNT ,HURTH>
		<SET CNT <+ .CNT 1>>)>
	 <COND (<IN-PARTY? ,UMBER>
		<PUT ,PARTY-SORT-TBL .CNT ,UMBER>
		<SET CNT <+ .CNT 1>>)>
	 <COND (<IN-PARTY? ,TAG>
		<PUT ,PARTY-SORT-TBL .CNT ,TAG>
		<SET CNT <+ .CNT 1>>)>
	 <COPYT ,PARTY-SORT-TBL ,PARTY <+ <* ,PARTY-MAX 2> 2>>>